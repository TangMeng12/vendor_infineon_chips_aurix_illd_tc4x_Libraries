
LCF_ICACHE_LINESIZE    = 64;        # core_config_icache_line_size=64
LCF_DCACHE_LINESIZE    = 64;        # core_config_dcache_line_size=64
LCF_VEC_STACKSIZE      = 0x10000;   # 64KB (use 50% of total size -- revisit?)
LCF_VEC_BUFFER_SIZE    = 0;         # 
LCF_STACKSIZE          = 0x00001000;# 4KB
LCF_IVTSIZE            = 400;       # irqs=84: (84+16)*4 = 400B
_HEAPSIZE              = 0;         # crt module sets this size for USE_LIBC case

MEMORY
{
    LMU9.CACHED        : ORIGIN = 0x90880000,                LENGTH = 256K          #Contains the data stored by PPU applications locally (cached address)
    LMU9.UNCACHED      : ORIGIN = 0xB0880000 + 256K,         LENGTH = 256K          #Contains the data shared by PPU applications and other TC applications (non cached address)
    PFLASH5.PPU        : ORIGIN = 0x81300000,                LENGTH = 1024K         #FLASH51 is reserved for PPU
    CSM                : ORIGIN = 0x92080000,                LENGTH = 512K          #Local memory for PPU. Used as cached address (Tricore shall not access this memory during runtime)
    VCCM               : ORIGIN = 0xC0000000,                LENGTH = 128K
    LMU_NOT_ALLOCATED1 : ORIGIN = 0xB0400000,                LENGTH = 2048K         #Define to set up MPU not allocated allocation is done by TriCore but PPU might read
    LMU_NOT_ALLOCATED2 : ORIGIN = 0xB0780000,                LENGTH = 1024K         #Define to set up MPU not allocated allocation is done by TriCore but PPU might read
    PER_SPACE_NO_ALOC1 : ORIGIN = 0xF0000000,                LENGTH = 256M          #Define to set up MPU not allocated allocation is done by TriCore but PPU might read
}

# MPU Permission flags
_MPU_PERMISSION_EXECUTE = 0x1;
_MPU_PERMISSION_READ    = 0x2;
_MPU_PERMISSION_WRITE   = 0x4;

_fMPU_REGION0 = ADDR(LMU9.CACHED);
_eMPU_REGION0 = ADDR(LMU9.CACHED) + SIZEOF(LMU9.CACHED);
_pMPU_REGION0 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_EXECUTE | _MPU_PERMISSION_WRITE);

_fMPU_REGION1 = ADDR(LMU9.UNCACHED);
_eMPU_REGION1 = ADDR(LMU9.UNCACHED) + SIZEOF(LMU9.UNCACHED);
_pMPU_REGION1 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_EXECUTE | _MPU_PERMISSION_WRITE);

_fMPU_REGION2 = ADDR(PFLASH5.PPU);
_eMPU_REGION2 = ADDR(PFLASH5.PPU)  + SIZEOF(PFLASH5.PPU);
_pMPU_REGION2 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_EXECUTE | _MPU_PERMISSION_WRITE);

_fMPU_REGION3 = ADDR(CSM);
_eMPU_REGION3 = ADDR(CSM) + SIZEOF(CSM);
_pMPU_REGION3 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_EXECUTE | _MPU_PERMISSION_WRITE);

_fMPU_REGION4 = ADDR(VCCM);
_eMPU_REGION4 = ADDR(VCCM) + SIZEOF(VCCM);
_pMPU_REGION4 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_WRITE);

_fMPU_REGION5 = ADDR(LMU_NOT_ALLOCATED1);
_eMPU_REGION5 = ADDR(LMU_NOT_ALLOCATED1) + SIZEOF(LMU_NOT_ALLOCATED1);
_pMPU_REGION5 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_WRITE);

_fMPU_REGION6 = ADDR(LMU_NOT_ALLOCATED2);
_eMPU_REGION6 = ADDR(LMU_NOT_ALLOCATED1) + SIZEOF(LMU_NOT_ALLOCATED1);
_pMPU_REGION6 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_WRITE);

_fMPU_REGION7 = ADDR(PER_SPACE_NO_ALOC1);
_eMPU_REGION7 = ADDR(PER_SPACE_NO_ALOC1) + SIZEOF(PER_SPACE_NO_ALOC1);
_pMPU_REGION7 = (_MPU_PERMISSION_READ | _MPU_PERMISSION_WRITE);

_fMPU_REGION8 = 0;
_eMPU_REGION8 = 0;
_pMPU_REGION8 = 0;

_fMPU_REGION9 = 0;
_eMPU_REGION9 = 0;
_pMPU_REGION9 = 0;

_fMPU_REGION10 = 0;
_eMPU_REGION10 = 0;
_pMPU_REGION10 = 0;

_fMPU_REGION11 = 0;
_eMPU_REGION11 = 0;
_pMPU_REGION11 = 0;

_fMPU_REGION12 = 0;
_eMPU_REGION12 = 0;
_pMPU_REGION12 = 0;

_fMPU_REGION13 = 0;
_eMPU_REGION13 = 0;
_pMPU_REGION13 = 0;

_fMPU_REGION14 = 0;
_eMPU_REGION14 = 0;
_pMPU_REGION14 = 0;

_fMPU_REGION15 = 0;
_eMPU_REGION15 = 0;
_pMPU_REGION15 = 0;

SECTIONS 
{

    #Vector table address as copy table information for TriCore to initialize the PPU vectors
    #during PPU Startup Trigger procedure
    #!!!! Please do not modify the location of below section !!!!
    GROUP BIND(ADDR(PFLASH5.PPU)) :
    {
        # Structure: SharedData_PpuIfConstants
        .shareddata_ppu_if_constants (LIT):
        {
            # SharedData_PpuIfConstants.ppuCopyTable from the file SharedData.h
            LONG(LOADADDR(.vectors));                                 # sourceAddress 
            LONG((ADDR(.vectors) & 0x0FFFFFFF) | 0xB0000000);         # destinationAddress
            LONG(SIZEOF(.vectors));                                   # size
            
            # SharedData_PpuIfConstants.ppuIpcDataPtr from the file SharedData.h 
            LONG(ADDR(.ppu_tc_shared_memory));
        }
    } > PFLASH5.PPU
    
    # RAM vector table requires 1024 byte alignment, note: not loaded
    GROUP BIND(ADDR(CSM)):
    {
        .vectors (TEXT) SIZE(DEFINED LCF_IVTSIZE ? LCF_IVTSIZE:240):
        {
            KEEP(*(.vectors));
        }
    } =0x78e0 > CSM AT>PFLASH5.PPU

    GROUP ALIGN(LCF_ICACHE_LINESIZE) BLOCK(4) :
    {
        _ftextarea = .;
        .text? : { *('.text$crt*') }
        * (TEXT): {}
        _etextarea = .;
        .padding ALIGN(LCF_ICACHE_LINESIZE) SIZE(2): {} = FILL(0x781E) # trap_s 0
    } = FILL(0x781E) > PFLASH5.PPU

    GROUP ALIGN(LCF_DCACHE_LINESIZE) BLOCK(4) :
    {
        # .lit and .rodata
        * (LIT): {}
        .initcall_cluster : {}
        .initcall_core : {}
        .destructcall_core : {}
        .padding ALIGN(LCF_DCACHE_LINESIZE) SIZE(2): {} = FILL(0x781E)
    } = FILL(0x781E) > PFLASH5.PPU

    GROUP ALIGN(LCF_DCACHE_LINESIZE) BLOCK(4):
    {
        .data :
        {  
            *(DATA)
            *(.user_ddr)
            *(.data*)
        }
        .padding ALIGN(LCF_DCACHE_LINESIZE) SIZE(2): {} = FILL(0x781E)
    } = FILL(0x781E) > LMU9.CACHED AT>PFLASH5.PPU
    
    GROUP ALIGN(LCF_DCACHE_LINESIZE) BLOCK(4):
    {
        .uncached.data : {
            *(.ucdata*)
            *(.uncached.data*)
        }
        .padding ALIGN(LCF_DCACHE_LINESIZE) SIZE(2): {} = FILL(0x781E)
    } > LMU9.UNCACHED AT>PFLASH5.PPU
    
    GROUP (NOLOAD) BLOCK(4):
    {
        *(BSS): { *(.tls*) }
    } > LMU9.CACHED
    
    GROUP (NOLOAD) ALIGN(4) BLOCK(4):
    {
        .ppu_tc_shared_memory (BSS): { * (.shared_bss_ppu_tc)}
        .uncached.bss: { * (.uncached.bss)}
    } > LMU9.UNCACHED

    # for now removed, as the current version of hal (speed) does not support sdata / sbss
    #GROUP ALIGN(LCF_DCACHE_LINESIZE) BLOCK(4):
    #{
    #    .sdata?: {}
    #} > CSM AT>PFLASH5.PPU
    
    #GROUP (NOLOAD) ALIGN(LCF_DCACHE_LINESIZE) BLOCK(4):
    #{
    #    .sbss?: {}
    #} > CSM

    GROUP (NOLOAD) BLOCK(4):
    {
        .heap? ALIGN(LCF_DCACHE_LINESIZE)
            SIZE(DEFINED _HEAPSIZE?_HEAPSIZE:0): {}
    } > CSM

    GROUP BLOCK(4):
    {
        _fstack = .;
        .stack0 (NOLOAD) ALIGN(LCF_DCACHE_LINESIZE)
             SIZE(DEFINED LCF_STACKSIZE?LCF_STACKSIZE:65536): {}
        _estack = .;
             //.stack SIZE(4): {}
        .local_storage0 (NOLOAD) ALIGN(LCF_DCACHE_LINESIZE): {}
    } > CSM
    
    GROUP ALIGN(LCF_DCACHE_LINESIZE) BLOCK(8):
    {
        .csm.data: {* (.csm.data) }
        .padding ALIGN(LCF_DCACHE_LINESIZE) SIZE(2): {} = FILL(0x781E)
    } = FILL(0x781E) > CSM AT>PFLASH5.PPU

    GROUP (NOLOAD) ALIGN(LCF_DCACHE_LINESIZE) BLOCK(8):
    {
        .stu_descriptors?:{}
        .csm.bss: {* (.csm.bss) }
    } > CSM 

    GROUP ALIGN(LCF_DCACHE_LINESIZE) BLOCK(8):
    {
        .vdata : {}
        # Deprecated .vecmem_data section, will be removed when all tools have
        # migrated to use ._vdata instead.
        .vecmem_data : {}
        .padding ALIGN(LCF_DCACHE_LINESIZE) SIZE(2): {} = FILL(0x781E)
    } = FILL(0x781E) > VCCM AT > PFLASH5.PPU

    GROUP BLOCK(8):
    {
        .vbss (NOLOAD): {}

        # The vector buffer section must be adjacent to the vector stack section
        .vbuffer (NOLOAD) SIZE(DEFINED LCF_VEC_BUFFER_SIZE ? LCF_VEC_BUFFER_SIZE : 0): {}
        # The vector stack must be the last section in VCCM, since we are allocating
        # all remaining VCCM here. We cannot use the SIZE() keyword for this though:
        # instead, we explicitly define the (otherwise implicitly defined) _fvstack
        # and _evstack symbols to indicate start and end of .vstack.
        # To avoid name collision with implicitly defined symbols, we call the
        # vstack section itself ._vstack.
        _fvstack = ALIGN(8);
        _fvecstack = _fvstack;
        ._vstack (NOLOAD) SIZE(DEFINED LCF_VEC_STACKSIZE ? LCF_VEC_STACKSIZE : 8192): {}
        _evstack = ALIGN(SIZEOF(VCCM)) - 8;
        _evecstack = _evstack;
    } > VCCM

}

#
# INITDATA
# The ARC does ROM-to-RAM copies.
# The INITDATA keyword is used to list the sections for ROM-to-RAM copy.
# NOTE: Do not list .vectors as it is handled by drivers/hal.
#
INITDATA  .data, .uncached.data, .vdata, .csm.data, .vecmem_data

_fstack1 = 0;
_estack1 = 0;
